el_line = _{ tag ~ id? ~ class* ~ (whitesp ~ attr)* ~(whitesp ~ cont_inline)? ~ ("\n" | EOI) ~ child_el* }
el_block = _{ tag ~ id? ~ class* ~ ":" ~ whitesp* ~ "\n" ~ cont_block+}
el = { el_line | el_block }
child_el    =  _{ PEEK_ALL ~ PUSH(indentation) ~ el ~ DROP }

ident = _{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHA_LOWER | "-")*}

tag = { (!("\n" | " ") ~ ASCII_ALPHA_LOWER)+ }

class_name = {  ident }
class = _{ "." ~ class_name }
id = _{ "#" ~ id_name }
id_name = {  ident }

attr = { attr_name ~ "=" ~ "\"" ~ attr_val ~ "\"" }
attr_name = { ident }
attr_val = { (!("\"") ~ ANY)+ }

cont_block = _{ PEEK_ALL ~ PUSH(indentation) ~ cont_block_line~ ("\n" | EOI) ~ DROP }
cont_block_line = {(!("\n") ~ ANY)+}
cont_inline = {(!("\n") ~ ANY)+ }


html         = _{ SOI ~ el+ ~ EOI }
whitesp = _{ " "+ }
indentation = _{ "  " }