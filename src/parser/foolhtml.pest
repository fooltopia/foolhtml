html = _{ SOI ~ el+ ~ eoi }

el = { el_line | el_block }
el_line = _{ tag ~ id? ~ class* ~ (whitesp ~ attr)* ~(whitesp ~ cont_inline)? ~ ("\n" | eoi) ~ child_el* }
el_block = _{ tag ~ id? ~ class* ~ (whitesp ~ attr)* ~ ":" ~ whitesp* ~ "\n" ~ cont_block+}
child_el = _{ PEEK_ALL ~ PUSH(indentation) ~ el ~ DROP }
cont_block = _{ PEEK_ALL ~ PUSH(indentation) ~ cont_block_line~ ("\n" | eoi) ~ DROP }
cont_block_line = {(!("\n") ~ ANY)+}
cont_inline = {(!("\n") ~ ANY)+ }

tag = { (!("\n" | " ") ~ ASCII_ALPHA_LOWER)+ }

class_name = {  ident }
class = _{ "." ~ class_name }
id = _{ "#" ~ id_name }
id_name = {  ident }

attr = { attr_name ~ "=" ~ attr_val  }
attr_name = { ident }
attr_val = _{ quoted_attr_val | naked_attr_val }
quoted_attr_val = _{ PUSH(opening_quote) ~ q_attr_val ~ POP}
naked_attr_val = _{ n_attr_val }
q_attr_val = { (!(PEEK) ~ ANY)+ }
n_attr_val = { (ASCII_ALPHA | ASCII_DIGIT | "-")+ }
opening_quote = _{ "\"" | "'" }

ident = _{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHA_LOWER | "-")*}

whitesp = _{ SPACE_SEPARATOR+ }
eol = _{ ("\n" | eoi) }
eoi = _{ !ANY }
indentation = _{ "  " }
